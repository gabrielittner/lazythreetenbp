import de.undercouch.gradle.tasks.download.Download
import org.gradle.internal.jvm.Jvm

apply plugin: 'java'
apply plugin: 'de.undercouch.download'
apply plugin: "com.vanniktech.maven.publish"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def version = project.hasProperty("tzdbVersion") ? project.property("tzdbVersion") : ""

task downloadTzdb(type: Download) {
    src "https://data.iana.org/time-zones/releases/tzdata${version}.tar.gz"
    dest file("${buildDir}/tzdb/${version}.tar.gz")

    doFirst {
        if (version == "") {
            throw new IllegalArgumentException("Need to specify tzdbVersion project property like 2017c")
        }
    }
}

task unzipTzdb(type: Copy, dependsOn: downloadTzdb) {
    from tarTree(resources.gzip(downloadTzdb.outputs.files.singleFile))
    into file("${buildDir}/tzdb-unpacked/$version")
}

task processTzdb(type: JavaExec, dependsOn: unzipTzdb) {
    def source = unzipTzdb.outputs.files.singleFile
    def target = rootProject.project("runtime").file("src/main")

    classpath sourceSets.main.runtimeClasspath
    main = "com.gabrielittner.threetenbp.LazyZoneRulesCompiler"
    args "--version", version, "--srcdir", source, "--codeoutdir", "$target/java", "--tzdboutdir", "$target/assets"
}

dependencies {
    implementation deps.threetenbp
    implementation deps.javapoet
    implementation deps.kotlinpoet
    implementation deps.google_options

    testImplementation deps.junit
    testImplementation deps.truth
    testImplementation deps.compile_testing
    testImplementation files(Jvm.current().getToolsJar())
}
